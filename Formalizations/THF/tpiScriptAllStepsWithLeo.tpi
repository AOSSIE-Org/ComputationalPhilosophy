tpi(com,write,'').
tpi(com,write,'%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%').
tpi(com,write,'%%% 1. Defining the embedding of Quantified Modal Logic KB in HOL (THF0)').
  thf(mu_type,type,(mu: $tType )).
  thf(mnot_type,type,(mnot: ( $i > $o ) > $i > $o )).
  thf(mnot,definition,( mnot = ( ^ [Phi: $i > $o,W: $i] : ~ ( Phi @ W ) ) )).
  thf(mor_type,type,( mor: ( $i > $o ) > ( $i > $o ) > $i > $o )).
  thf(mor,definition,( mor = ( ^ [Phi: $i > $o,Psi: $i > $o,W: $i] : ( ( Phi @ W ) | ( Psi @ W ) ) ) )).
  thf(mbox_generic_type,type,( mbox_generic: ( $i > $i > $o ) > ( $i > $o ) > $i > $o )).
  thf(mbox_generic,definition,( mbox_generic = ( ^ [R: $i > $i > $o,Phi: $i > $o,W: $i] : ! [V: $i] : ( ~ ( R @ W @ V ) | ( Phi @ V ) ) ) )).
  thf(mforall_ind_type,type,( mforall_ind: ( mu > $i > $o ) > $i > $o )).
  thf(mforall_ind,definition,( mforall_ind = ( ^ [Phi: mu > $i > $o,W: $i] : ! [X: mu] : ( Phi @ X @ W ) ) )).
  thf(mforall_indset_type,type,( mforall_indset: ( ( mu > $i > $o ) > $i > $o ) > $i > $o )).
  thf(mforall_indset,definition,( mforall_indset = ( ^ [Phi: ( mu > $i > $o ) > $i > $o,W: $i] : ! [X: mu > $i > $o] : ( Phi @ X @ W ) ) )).
  thf(mand_type,type,( mand: ( $i > $o ) > ( $i > $o ) > $i > $o )).
  thf(mand,definition, ( mand = ( ^ [Phi: $i > $o,Psi: $i > $o] : ( mnot @ ( mor @ ( mnot @ Phi ) @ ( mnot @ Psi ) ) ) ) )).
  thf(mimplies_type,type,( mimplies: ( $i > $o ) > ( $i > $o ) > $i > $o )).
  thf(mimplies,definition,( mimplies = ( ^ [Phi: $i > $o,Psi: $i > $o] : ( mor @ ( mnot @ Phi ) @ Psi ) ) )).
  thf(mequiv_type,type,( mequiv: ( $i > $o ) > ( $i > $o ) > $i > $o )).
  thf(mequiv,definition, ( mequiv = ( ^ [Phi: $i > $o,Psi: $i > $o] : ( mand @ ( mimplies @ Phi @ Psi ) @ ( mimplies @ Psi @ Phi ) ) ) )).
  thf(mdia_generic_type,type,( mdia_generic: ( $i > $i > $o ) > ( $i > $o ) > $i > $o )).
  thf(mdia_generic,definition,( mdia_generic = ( ^ [R: $i > $i > $o,Phi: $i > $o] : ( mnot @ ( mbox_generic @ R @ ( mnot @ Phi ) ) ) ) )).
  thf(mexists_ind_type,type,( mexists_ind: ( mu > $i > $o ) > $i > $o )).
  thf(mexists_ind,definition,( mexists_ind = ( ^ [Phi: mu > $i > $o] : ( mnot @ ( mforall_ind @ ^ [X: mu] : ( mnot @ ( Phi @ X ) ) ) ) ) )).
  thf(v_type,type,(v: ( $i > $o ) > $o )).
  thf(mvalid,definition,( v = ( ^ [Phi: $i > $o] : ! [W: $i] : ( Phi @ W ) ) )).
  thf(rel_type,type,( rel: $i > $i > $o )).
  thf(mbox_type,type,( mbox: ( $i > $o ) > $i > $o )).
  thf(mbox,definition,( mbox = ( mbox_generic @ rel ) )).
  thf(mdia_type,type,( mdia: ( $i > $o ) > $i > $o )).
  thf(mdia,definition,( mdia = ( mdia_generic @ rel ) )).
tpi(com,write,'').
tpi(com,output,stdout).
tpi(com,write,'%%%    Checking Satisfiability of this embedding.').
tpi(com,execute,'SZS_STATUS' = 'Nitrox---2013 20 $getgroups(tpi)').
tpi(com,write,'%%%    SZS_STATUS for the embedding is ' & '$getenv(SZS_STATUS)').
tpi(com,assert,$getenv('SZS_STATUS') = 'Satisfiable').
tpi(com,write,'%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%').
tpi(com,write,'').

tpi(com,write,'%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%').
tpi(com,write,'%%% 2. Adding all Axioms and Definitions of the proof of Goedel.').
tpi(com,start_group,goedelAssumptions).
  thf(p_tp,type,(p: ( mu > $i > $o ) > $i > $o )).
  thf(g_tp,type,(g: mu > $i > $o )).
  thf(ess_tp,type,(ess: ( mu > $i > $o ) > mu > $i > $o )).
  thf(ne_tp,type,(ne: mu > $i > $o )).
  thf(axA1,axiom,( v @ ( mforall_indset @ ^ [Phi: mu > $i > $o] : ( mequiv @ ( p @ ^ [X: mu] : ( mnot @ ( Phi @ X ) ) ) @ ( mnot @ ( p @ Phi ) ) ) ) )).
  thf(axA2,axiom,( v @ ( mforall_indset @ ^ [Phi: mu > $i > $o] : ( mforall_indset @ ^ [Psi: mu > $i > $o] : ( mimplies @ ( mand @ ( p @ Phi ) @ ( mbox @ ( mforall_ind @ ^ [X: mu] : ( mimplies @ ( Phi @ X ) @ ( Psi @ X ) ) ) ) ) @ ( p @ Psi ) ) ) ) )).
  thf(defD1,definition,( g = ( ^ [X: mu] : ( mforall_indset @ ^ [Phi: mu > $i > $o] : ( mimplies @ ( p @ Phi ) @ ( Phi @ X ) ) ) ) )).
  thf(axA3,axiom,( v @ ( p @ g ) )).
  thf(axA4,axiom,( v @ ( mforall_indset @ ^ [Phi: mu > $i > $o] : ( mimplies @ ( p @ Phi ) @ ( mbox @ ( p @ Phi ) ) ) ) )).
  thf(defD2,definition,( ess = ( ^ [Phi: mu > $i > $o,X: mu] : ( mand @ ( Phi @ X ) @ ( mforall_indset @ ^ [Psi: mu > $i > $o] : ( mimplies @ ( Psi @ X ) @ ( mbox @ ( mforall_ind @ ^ [Y: mu] : ( mimplies @ ( Phi @ Y ) @ ( Psi @ Y ) ) ) ) ) ) ) ) )).
  thf(defD3,definition,( ne = ( ^ [X: mu] : ( mforall_indset @ ^ [Phi: mu > $i > $o] : ( mimplies @ ( ess @ Phi @ X ) @ ( mbox @ ( mexists_ind @ ^ [Y: mu] : ( Phi @ Y ) ) ) ) ) ) )).
  thf(axA5,axiom,( v @ ( p @ ne ) )).
tpi(com,end_group,goedelAssumptions).
tpi(com,write,'%%%    Group of Axioms defined; this group is called: goedelAssumptions').
tpi(com,write,'').
tpi(com,output,stdout).
tpi(com,write,'%%%    Checking Satisfiability of goedelAssumptions.').
tpi(com,execute,'SZS_STATUS' = 'Nitrox---2013 60 $getgroups(tpi)').
tpi(com,write,'%%%    SZS_STATUS for goedelAssumptions is ' & '$getenv(SZS_STATUS)').
tpi(com,assert,$getenv('SZS_STATUS') = 'Satisfiable').
tpi(com,write,'%%%    Deactivating group goedelAssumptions again.').
tpi(com,write,'%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%').
tpi(com,write,'').

tpi(com,write,'%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%').
tpi(com,write,'%%% 3. Analysing theorem thmT1.').
tpi(com,deactivate_group,goedelAssumptions).
tpi(com,write,'%%%    Activating exactly axioms axA1 and axA2.').
tpi(com,activate,p_tp).
tpi(com,activate,axA1).
tpi(com,activate,axA2).
  thf(thmT1_con,conjecture,( v @ ( mforall_indset @ ^ [Phi: mu > $i > $o] : ( mimplies @ ( p @ Phi ) @ ( mdia @ ( mexists_ind @ ^ [X: mu] : ( Phi @ X ) ) ) ) ) )).
tpi(com,write,'').
tpi(com,output,stdout).
tpi(com,write,'%%%    Checking whether thmT1 is a theorem (using LEO-II).').
tpi(com,execute,'SZS_STATUS' = 'LEO-II---1.6.0 20 $getgroups(tpi)').
tpi(com,write,'%%%    SZS_STATUS for thmT1 is ' & '$getenv(SZS_STATUS)').
tpi(com,assert,$getenv('SZS_STATUS') = 'Theorem').
tpi(com,write,'%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%').
tpi(com,write,'').

tpi(com,write,'%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%').
tpi(com,write,'%%% 4. Analysing theorem corC.').
tpi(com,deactivate_group,goedelAssumptions).
tpi(com,deactivate,axA1).
tpi(com,deactivate,axA2).
tpi(com,write,'%%%    Activating exactly definition defD1, axA3 and theorem/lemma thmT1.').
tpi(com,activate,p_tp).
tpi(com,activate,g_tp).
tpi(com,activate,defD1).
tpi(com,activate,axA3).
tpi(com,set_role,thmT1_con = lemma).
  thf(corC_con,conjecture,( v @ ( mdia @ ( mexists_ind @ ^ [X: mu] : ( g @ X ) ) ) )).
tpi(com,write,'').
tpi(com,output,stdout).
tpi(com,write,'%%%    Checking whether corC is a theorem (using LEO-II).').
tpi(com,execute,'SZS_STATUS' = 'LEO-II---1.6.0 20 $getgroups(tpi)').
tpi(com,write,'%%%    SZS_STATUS for corC is ' & '$getenv(SZS_STATUS)').
tpi(com,assert,$getenv('SZS_STATUS') = 'Theorem').
tpi(com,write,'%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%').
tpi(com,write,'').

tpi(com,write,'%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%').
tpi(com,write,'%%% 5. Analysing theorem thmT2.').
tpi(com,deactivate_group,goedelAssumptions).
tpi(com,deactivate,thmT1_con).
tpi(com,deactivate,corC_con).
tpi(com,write,'%%%    Activating exactly axA1, defD1, axA4 and defD2.').
tpi(com,activate,p_tp).
tpi(com,activate,g_tp).
tpi(com,activate,ess_tp).
tpi(com,activate,axA1).
tpi(com,activate,defD1).
tpi(com,activate,axA4).
tpi(com,activate,defD2).
  thf(thmT2_con,conjecture,( v @ ( mforall_ind @ ^ [X: mu] : ( mimplies @ ( g @ X ) @ ( ess @ g @ X ) ) ) )).
tpi(com,write,'').
tpi(com,output,stdout).
tpi(com,write,'%%%    Checking whether thmT2 is a theorem (using LEO-II).').
tpi(com,execute,'SZS_STATUS' = 'LEO-II---1.6.0 20 $getgroups(tpi)').
tpi(com,write,'%%%    SZS_STATUS for thmT2 is ' & '$getenv(SZS_STATUS)').
tpi(com,assert,$getenv('SZS_STATUS') = 'Theorem').
tpi(com,write,'%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%').
tpi(com,write,'').

tpi(com,write,'%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%').
tpi(com,write,'%%% 6. Analysing theorem thmT3.').
tpi(com,deactivate_group,goedelAssumptions).
tpi(com,deactivate,thmT1_con).
tpi(com,deactivate,corC_con).
tpi(com,deactivate,thmT2_con).
tpi(com,write,'   Activating exactly defD1, corC, thmT2, defD3, axA5').
tpi(com,activate,p_tp).
tpi(com,activate,g_tp).
tpi(com,activate,ess_tp).
tpi(com,activate,ne_tp).
tpi(com,activate,defD1).
tpi(com,activate,corC_con).
tpi(com,set_role,corC_con = lemma).
tpi(com,activate,thmT2_con).
tpi(com,set_role,thmT2_con = lemma).
tpi(com,activate,defD3).
tpi(com,activate,axA5).
tpi(com,write,'   Adding axiom B (symmetry).').
  thf(msymmetric_type,type,( msymmetric: ( $i > $i > $o ) > $o )).
  thf(msymmetric,definition,( msymmetric = ( ^ [R: $i > $i > $o] : ! [S: $i,T: $i] : ( ( R @ S @ T ) => ( R @ T @ S ) ) ) )).
  thf(sym,axiom, ( msymmetric @ rel )).
  thf(thmT3_con,conjecture,( v @ ( mbox @ ( mexists_ind @ ^ [X: mu] : ( g @ X ) ) ) )).
tpi(com,write,'').
tpi(com,output,stdout).
tpi(com,write,'   Checking whether thmT3 is a theorem (using LEO-II).').
tpi(com,execute,'SZS_STATUS' = 'LEO-II---1.6.0 20 $getgroups(tpi)').
tpi(com,write,'   SZS_STATUS for thmT3 is ' & '$getenv(SZS_STATUS)').
tpi(com,assert,$getenv('SZS_STATUS') = 'Theorem').
tpi(com,write,'%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%').
tpi(com,write,'').

tpi(end,exit,exit).